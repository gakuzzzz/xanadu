apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: "eclipse"

sourceCompatibility = 1.8
version = '0.1.0-SNAPSHOT'
group = 'jp.t2v'
def srcJava = 'src/main/java'
def srcDelomboked = 'build/src-delomboked/main/java'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

configurations {
    provided
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:java-hamcrest:2.0.0.0"
    provided "org.projectlombok:lombok:1.16.2"
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

task delombok {
    inputs.files file(srcJava)
    outputs.dir file(srcDelomboked)

    doLast {
        FileCollection collection = files(configurations.compile)
        FileCollection sumTree = collection + fileTree(dir: 'bin')

        ant.taskdef(name: 'delombok', classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.provided.asPath)
        ant.delombok(from:srcJava, to:srcDelomboked, classpath: sumTree.asPath)
    }
}

javadoc {
    dependsOn delombok
    source = srcDelomboked
    classpath = sourceSets.main.compileClasspath
    options {
        links "https://docs.oracle.com/javase/8/docs/api/"
        links "http://projectlombok.org/api/"
    }
}


modifyPom {
    project {
        url 'https://github.com/gakuzzzz/xanadu'
        scm {
            url 'https://github.com/gakuzzzz/xanadu'
            connection 'scm:git:git@github.com:gakuzzzz/xanadu.git'
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'gakuzzzz'
                name 'gakuzzzz'
                url 'https://github.com/gakuzzzz'
            }
        }
    }
}


wrapper {
    gradleVersion = "2.3"
}